##### restrict-gce-machine-type.sentinel #####
# This policy uses the Sentinel tfplan import to require that
# all Google compute instances have machine types from an allowed list

##### Imports #####

import "tfplan"
import "strings"

##### Functions #####

# Find all resources of a specific type
# from all modules using the tfplan import
find_resources_from_plan = func(type) {

  # Start with an empty map
  resource_maps = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {

    # Append all resources of the specified type to resource_maps
    # setting the key to the module path. Set the empty map, {}, If the
    # module does not have any resources of the specified type.
    resource_maps[path] = tfplan.module(path).resources[type] else {}
  }

  # return resource_maps
  return resource_maps
}

# Get the full address of a resource instance including modules, type,
# name, and index in form module.<A>.module.<B>.<type>.<name>[<index>]
# module_path is like module paths returned by tfplan.module_paths
get_instance_address = func(module_path, type, name, index) {

  # Handle root and non-root modules differently
  if length(module_path) == 0 {
    # root module
    address = type + "." + name + "[" + string(index) + "]"
  } else {
    # non-root module
    address = "module." + strings.join(module_path, ".module.") + "." + type + "." + name + "[" + string(index) + "]"
  }

  return address

}

# Validate that all GCP compute instances have machine_type
# in allowed_types list
validate_machine_types = func(allowed_types) {

  # Initialize validated to true
  # This will be set to false if any instances violate rule
  validated = true

  # Set resource_type
  resource_type = "google_compute_instance"

  # Get all resources of specified type
  resource_maps = find_resources_from_plan(resource_type)

  # Loop through the module-level resource maps
  for resource_maps as module_path, resource_map {
    # Loop through the named resources
    for resource_map as name, instances {
      # Loop through resource instances
      for instances as index, r {

        # Get addresses of the resource and the instance
        address = get_instance_address(module_path, resource_type, name, index)

        # Skip resources that are being destroyed
        # to avoid unnecessary policy violations
        if length(r.diff) == 0 {
          print("Skipping resource", address,
            "that is being destroyed.")
          continue
        }

        # Validate that each instance has allowed value
        # If not, print violation message
        if r.applied.machine_type not in allowed_types {
          print("GCP compute instance", address,
            "has attribute", r.applied.machine_type,
            "that is not in the list", allowed_types)
          validated = false
        }

      } // end resource instances
    } // end named resources
  } // end resorce maps

  # Return validated which will be false if there were any violators
  return validated
}

##### Lists #####

# Allowed GCP Machine Types
# We don't include n1-standard-1 to illustrate overriding failed policy
allowed_machine_types = [
  "n1-standard-2",
  "n1-standard-4",
]

##### Rules #####

# Rule to restrict GCP compute machine types
machine_type_allowed = rule {
  validate_machine_types(allowed_machine_types)
}

# Main rule that requires other rules to be true
main = rule {
  machine_type_allowed
}
