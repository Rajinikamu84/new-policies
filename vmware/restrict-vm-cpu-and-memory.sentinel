##### restrict-vm-cpu-and-memory.sentinel #####
# This policy uses the Sentinel tfplan import to require that
# all VMware VMs obey CPU and memory limits

##### Imports #####

import "tfplan"
import "strings"

##### Functions #####

# Find all resources of a specific type
# from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resource_maps = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {

    # Compute joined_path from the module path
    if length(path) == 0 {
      joined_path = ""
    } else {
      joined_path = "module." + strings.join(path, ".module.") + "."
    }

    # Append all resources of the specified type to resource_maps
    # setting the key to joined_path. Append the empty map, {}, if the
    # module does not have any resources of the specified type.
    resource_maps[joined_path] = tfplan.module(path).resources[type] else {}
  }

  return resource_maps
}

# Get the full address of a resource instance including modules, type,
# name, and index in form module.<A>.module.<B>.<type>.<name>[<index>]
# module_path is like module paths returned by tfplan.module_paths
get_instance_address = func(joined_path, type, name, index) {
  address = joined_path + type + "." + name + "[" + string(index) + "]"
  return address
}

# Validate that all VMware VMs have obey CPU and memory limits
validate_cpu_and_memory_limits = func(cpu_limit, memory_limit) {

  # Initialize booleans to true
  # They will be set to false if any instances violate rules
  valid_cpu = true
  valid_memory = true

  # Set resource_type
  resource_type = "vsphere_virtual_machine"

  # Get all resources of specified type
  resource_maps = find_resources_from_plan(resource_type)

  # Loop through the module-level resource maps
  for resource_maps as module_path, resource_map {
    # Loop through the named resources
    for resource_map as name, instances {
      # Loop through resource instances
      for instances as index, r {

        # Get addresses of the resource and the instance
        address = get_instance_address(module_path, resource_type, name, index)

        # Skip resources that are being destroyed
        # to avoid unnecessary policy violations
        if length(r.diff) == 0 {
          print("Skipping resource", address,
            "that is being destroyed.")
          continue
        }

        # Determine if the attribute is computed
        if r.diff["num_cpus"].computed else false is true {
          # Print message indicating a computed value was found
          print("resource", address,
            "has attribute, num_cpus, that is computed.")
          # If you want computed values to cause the policy to fail, uncomment the next line.
          #valid_cpu = false
        } else {
          # Validate that each instance has valid number of CPUs
          # If not, print violation message
          if int(r.applied.num_cpus) > cpu_limit {
            print("Virtual machine", address,
              "has", r.applied.num_cpus, "CPUs,",
              "which is greater than the limit", cpu_limit)
            valid_cpu = false
          }
        } // end cpus computed check

        # Determine if the attribute is computed
        if r.diff["memory"].computed else false is true {
          # Print message indicating a computed value was found
          print("resource", address,
            "has attribute, memory, that is computed.")
          # If you want computed values to cause the policy to fail, uncomment the next line.
          #valid_memory = false
        } else {
          # Validate that each instance has valid amount of memory
          # If not, print violation message
          if int(r.applied.memory) > memory_limit {
            print("Virtual machine", address,
              "has", r.applied.memory,
              "MB of memory, which is greater than the limit", memory_limit)
            valid_memory = false
          }
        } // end memory computed check

      } // end resource instances
    } // end named resources
  } // end resorce maps

  # Return booleans which will be false if there were any violations
  return [valid_cpu, valid_memory]
}

##### Rules #####

# Call the validation function and assign results
validations = validate_cpu_and_memory_limits(4, 8192)
valid_cpu = validations[0]
valid_memory = validations[1]

# Main rule
main = rule {
  valid_cpu is true and valid_memory is true
}
